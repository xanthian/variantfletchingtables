plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    //maven { url "https://jitpack.io" }
    maven { url "https://cursemaven.com/" }
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://maven.terraformersmc.com/" } // Biolith + ModMenu (Cinderscapes)
    maven { url "https://maven.minecraftforge.net/" } // Terrablender
    maven { url "https://maven.shedaniel.me" } // ClothConfig (Cinderscapes)
    maven { url "https://maven.kyrptonaught.dev" } // Deeper & Darker
    maven { url "https://maven.wispforest.io" } // OwO-Lib (Deeper & Darker)
    maven { url "https://maven.ladysnake.org/releases" } // Trinkets, Cardinal Components (Desolation)
    maven { url "https://raw.githubusercontent.com/Fuzss/modresources/main/maven"} // Fuzs stuff
}

loom {
    accessWidenerPath = file("src/main/resources/variantfletchingtables.accesswidener")
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=variantfletchingtables"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Blockus 2.9.1+1.21
    modRuntimeOnly "maven.modrinth:blockus:LSQaGN5A"

    // Cinderscapes v5.0.0 for 1.21
    modRuntimeOnly "com.terraformersmc:cinderscapes:5.0.0"
    modRuntimeOnly "com.terraformersmc:cinderscapes-client:5.0.0"
    modRuntimeOnly "com.terraformersmc:cinderscapes-common:5.0.0"
    modRuntimeOnly "com.terraformersmc:cinderscapes-worldgen:5.0.0"
    modApi "com.terraformersmc:biolith-fabric:3.0.0-alpha.1"
    modApi "com.terraformersmc:modmenu:11.0.1"
    modApi "com.terraformersmc.terraform-api:terraform-biome-remapper-api-v1:11.0.0-alpha.1"
    modApi "com.terraformersmc.terraform-api:terraform-shapes-api-v1:11.0.0-alpha.1"
    modApi "com.terraformersmc.terraform-api:terraform-wood-api-v1:11.0.0-alpha.1"
    modApi "me.shedaniel.cloth:cloth-config-fabric:15.0.127"

    // Deeper and Darker 1.31-1.21
    modRuntimeOnly "maven.modrinth:deeperdarker:zzgUVMQw"
    modApi "net.kyrptonaught:customportalapi:0.0.1-beta66-1.21"
    modApi "io.wispforest:owo-lib:0.12.10+1.21"

    // Desolation 1.7.0
    modRuntimeOnly "maven.modrinth:desolation:5yL0TXxE"
    modApi "dev.emi:trinkets:3.10.0"
    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:6.1.1"
    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:6.1.1"
    modApi "com.terraformersmc.terraform-api:terraform-tree-api-v1:11.0.0-alpha.1"
    modApi "maven.modrinth:geckolib:11VBhLU2" // Geckolib 4.5.8

    // Natures Spirit 1.5.3-1.21
    modRuntimeOnly "maven.modrinth:natures-spirit:k2mup3Kl"

    // Regions Unexplored 0.5.6.1
    modRuntimeOnly "maven.modrinth:regions-unexplored:bHIj876h"
    modApi "com.github.glitchfiend:TerraBlender-fabric:1.21-4.0.0.2" // Terrablender
    api "fuzs.forgeconfigapiport:forgeconfigapiport-common-neoforgeapi:21.0.8"
    api "fuzs.forgeconfigapiport:forgeconfigapiport-common-forgeapi:21.0.8"
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:21.0.8"

    // Tech Reborn
    modRuntimeOnly "TechReborn:TechReborn-1.21:5.11.6"

}

base {
    archivesName = project.archives_base_name
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}
